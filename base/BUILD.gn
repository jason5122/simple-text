source_set("base") {
  sources = [
    "check.cc",
    "files/file_path.cc",
    "files/file_reader.cc",
    "files/file_util.cc",
    "path_service.cc",
    "strings/string_util.cc",
    "strings/utf_string_conversions.cc",
  ]
  deps = [ "//third_party/spdlog" ]

  # TODO: Consider checking all type conversions. These result in silent and subtle bugs. We
  # previously spent one hour debugging one.
  # configs += [ "//build/config/compiler:prevent_unsafe_narrowing" ]

  libs = []
  frameworks = []

  if (is_posix) {
    sources += [
      "files/file_posix.cc",
      "files/file_util_posix.cc",
    ]
  }

  if (is_mac) {
    sources += [
      "apple/foundation_util.mm",
      "apple/owned_nsevent.mm",
      "files/file_util_mac.mm",
      "path_service_mac.cc",
      "strings/sys_string_conversions_mac.mm",
    ]
    frameworks += [
      "Foundation.framework",
      "CoreServices.framework",
    ]
  }

  if (is_linux) {
    sources += [ "path_service_linux.cc" ]
  }

  if (is_win) {
    sources += [
      "files/file_util_win.cc",
      "path_service_win.cc",
      "strings/sys_string_conversions_win.cc",
    ]
    libs += [ "shell32.lib" ]
  }
}

source_set("base_unittests") {
  testonly = true

  sources = [
    "containers/fixed_flat_map_unittest.cc",
    "containers/flat_map_unittest.cc",
    "containers/flat_tree_unittest.cc",
    "files/file_path_unittest.cc",
    "hash/hash_unittest.cc",
    "location_unittest.cc",
    "memory/weak_ptr_unittest.cc",
    "numeric/safe_conversions_unittest.cc",
    "numeric/saturation_arithmetic_unittest.cc",
    "strings/string_util_unittest.cc",
    "strings/utf_string_conversions_unittest.cc",
  ]

  if (is_mac) {
    sources += [
      "apple/foundation_util_unittest.mm",
      "apple/scoped_cftyperef_unittest.cc",
      "strings/sys_string_conversions_mac_unittest.mm",
    ]
  }
  if (is_win) {
    sources += [ "strings/sys_string_conversions_win_unittest.cc" ]
  }

  deps = [
    ":base",
    "//third_party/googletest:gtest",
  ]
}

source_set("base_perftests") {
  testonly = true

  sources = [ "files/file_reader_perftest.cc" ]

  deps = [
    ":base",
    "//third_party/googletest:gtest",
  ]
}
