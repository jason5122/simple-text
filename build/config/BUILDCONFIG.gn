# =============================================================================
# PLATFORM SELECTION
# =============================================================================
if (target_os == "") {
  target_os = host_os
}
if (target_cpu == "") {
  target_cpu = host_cpu
}

if (current_cpu == "") {
  current_cpu = target_cpu
}
if (current_os == "") {
  current_os = target_os
}

# =============================================================================
# BUILD FLAGS
# =============================================================================
declare_args() {
  is_release = false
}

# =============================================================================
# OS DEFINITIONS
# =============================================================================
is_mac = target_os == "mac"
is_linux = target_os == "linux"
is_win = target_os == "win"
is_posix = !is_win

# =============================================================================
# TARGET DEFAULTS
# =============================================================================
default_compiler_configs = [
  "//build/config/compiler:compiler",
  "//build/config/compiler:default_include_dirs",
  "//build/config/compiler:default_optimization",
  "//build/config/compiler:no_exceptions",
  "//build/config/compiler:simple_text_code",
  "//build/config/compiler:no_rtti",
  "//build/config/compiler:runtime_library",
  "//build/config/sanitizers:default_sanitizer_flags",
]

if (is_mac) {
  default_compiler_configs += [ "//build/config/mac:enable_arc" ]
}
if (is_win) {
  default_compiler_configs += [
    "//build/config/win:unicode",
    "//build/config/win:lean_and_mean",
    "//build/config/win:nominmax",
    "//build/config/win:winver",
    "//build/config/win:no_crt_deprecation_warnings",
  ]
}
if (is_release) {
  default_compiler_configs += [ "//build/config/compiler:release" ]
}

_linker_configs = []
if (is_win) {
  # Default to console-mode apps. Most of our targets don't use the windows subsystem.
  _linker_configs += [ "//build/config/win:console" ]
}

set_defaults("static_library") {
  configs = default_compiler_configs
}
set_defaults("source_set") {
  configs = default_compiler_configs
}
set_defaults("shared_library") {
  configs = default_compiler_configs
}
set_defaults("loadable_module") {
  configs = default_compiler_configs
}
set_defaults("executable") {
  configs = default_compiler_configs + _linker_configs
}

set_default_toolchain("//build/toolchain/$target_os:clang")

# A helper for forwarding testonly and visibility.
# Forwarding "*" does not include variables from outer scopes (to avoid copying all globals into
# each template invocation), so it will not pick up file-scoped or outer-template-scoped variables.
# Normally this behavior is desired, but "visibility" and "testonly" are commonly defined in outer
# scopes. Explicitly forwarding them in forward_variables_from() works around this nuance.
TESTONLY_AND_VISIBILITY = [
  "testonly",
  "visibility",
]

# Set libc++ as a dependency for executable and shared_library targets.
foreach(_target_type,
        [
          "executable",
          "loadable_module",
          "shared_library",
        ]) {
  template(_target_type) {
    # Alias "target_name" because it is clobbered by forward_variables_from().
    _target_name = target_name
    target(_target_type, _target_name) {
      forward_variables_from(invoker, "*", TESTONLY_AND_VISIBILITY)
      forward_variables_from(invoker, TESTONLY_AND_VISIBILITY)
      if (!defined(inputs)) {
        inputs = []
      }

      if (!defined(deps)) {
        deps = []
      }

      deps += [ "//third_party/llvm:libc++" ]
    }
  }
}
