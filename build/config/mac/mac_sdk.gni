declare_args() {
  # This variable corresponds to the `MACOSX_DEPLOYMENT_TARGET` define. Changing this controls which
  # symbols the macOS SDK marks as available (via `__builtin_available` and `@available`) and
  # deprecated (producing a warning if called).
  #
  # This also controls the value of the `LSMinimumSystemVersion` in `Info.plist` files. This value
  # controls the minimum OS version that may launch the application, and OS releases older than this
  # will refuse to launch the application.
  mac_deployment_target = "11.0"

  # Minimum supported version of the Mac SDK.
  mac_sdk_min = "15"

  # Path to a specific version of the Mac SDK, not including a slash at the end.
  mac_sdk_path = ""
}

if (mac_sdk_path == "") {
  find_sdk_args = [ mac_sdk_min ]
  mac_sdk_path = exec_script("//build/config/mac/find_sdk.py",
                             find_sdk_args,
                             "trim string")
}
