import("//build/config/linux/pkg_config.gni")

source_set("font") {
  sources = [ "font_rasterizer.cc" ]

  deps = [
    "//third_party/fmt",
    "//unicode",
  ]

  if (is_mac) {
    sources += [ "font_rasterizer_core_text.cc" ]
    frameworks = [
      "CoreText.framework",
      "CoreGraphics.framework",
      "CoreFoundation.framework",
    ]
  }

  if (is_linux) {
    sources += [ "font_rasterizer_pango.cc" ]
    configs += [ ":linux_font_libs" ]
  }

  if (is_win) {
    deps = [ "directwrite" ]
  }
}

if (is_linux) {
  pkg_config("linux_font_libs") {
    packages = [ "pangocairo" ]
  }
}

source_set("font_unittests") {
  testonly = true

  sources = [ "font_rasterizer_unittest.cc" ]

  deps = [
    ":font",
    "//third_party/googletest:gtest",
  ]
}
