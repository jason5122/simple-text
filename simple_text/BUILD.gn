import("//build/config/mac/icon.gni")
import("//build/config/mac/mac_app_bundle.gni")

common_sources = [
  "editor_app.cc",
  "editor_window.cc",
  "main.cc",
]

common_deps = [
  "//base",
  "//gl",
  "//gui",
  "//third_party/spdlog",

  # TODO: Find a way to include Tree-sitter headers without listing the dependency here.
  # "//third_party/tree_sitter",
]

if (is_mac) {
  mac_app_bundle("simple_text") {
    output_name = "Simple Text"
    bundle_identifier = "com.jason5122.simple-text"
    info_plist = "resources/Info.plist"

    sources = common_sources
    deps = common_deps

    # deps += [ ":icon" ]
  }

  icon("icon") {
    sources = [ "resources/app.iconset" ]
  }
}

if (is_linux || is_win) {
  executable("simple_text") {
    sources = common_sources
    deps = common_deps

    # Required for CoInitializeEx()/CoUninitialize().
    # TODO: Consider moving this to //gui/win32/.
    if (is_win) {
      configs -= [ "//build/config/win:console" ]
      configs += [ "//build/config/win:windowed" ]

      libs = [ "ole32.lib" ]
    }
  }
}
